// Property of Lucian Tranc

// When using a template ush file, we need the _{ParameterName} appendix on
// global functions and parameters, because the template can be included multiple
// times for different data interfaces in a system.

StructuredBuffer<float4> {ParameterName}_UVRects;
uint {ParameterName}_NumRects;

// Additional buffers for text metrics
StructuredBuffer<uint>  {ParameterName}_TextUnicode;
StructuredBuffer<float2> {ParameterName}_CharacterPositions;
StructuredBuffer<uint>  {ParameterName}_LineIndices;
StructuredBuffer<uint>  {ParameterName}_LineCharacterCounts;
uint {ParameterName}_NumChars;


void GetCharacterUV_{ParameterName}(in int In_CharacterIndex, out float Out_USize, out float Out_VSize, out float Out_UStart, out float Out_VStart)
{

	int Unicode = 0;
	Unicode = {ParameterName}_TextUnicode[In_CharacterIndex];

	if (Unicode >= 0 && Unicode < {ParameterName}_NumRects)
	{
		float4 r = {ParameterName}_UVRects[Unicode];
		Out_USize  = r.x;
		Out_VSize  = r.y;
		Out_UStart = r.z;
		Out_VStart = r.w;
	}
	else
	{
		Out_USize = 0.0f;
		Out_VSize = 0.0f;
		Out_UStart = 0.0f;
		Out_VStart = 0.0f;
	}
}

// Returns the character position (Position/float3) at In_CharacterIndex relative to the center of the text
// Coordinate mapping: X(forward)=0, Y(left/right)=horizontal, Z(up/down)=vertical
void GetCharacterPosition_{ParameterName}(in int In_CharacterIndex, out float3 Out_CharacterPosition)
{
	if ({ParameterName}_NumChars == 0)
	{
		Out_CharacterPosition = float3(0.0f, 0.0f, 0.0f);
		return;
	}

	int idx = In_CharacterIndex;
	idx = clamp(idx, 0, int({ParameterName}_NumChars) - 1);

	float2 p = {ParameterName}_CharacterPositions[idx];

	// see UNTPDataInterface::GetCharacterPositionVM for info on why these are flipped
	Out_CharacterPosition = float3(0.0f, -p.x, -p.y);
}

// Returns the number of characters in InputText
void GetTextCharacterCount_{ParameterName}(out int Out_CharacterCount)
{
	Out_CharacterCount = int({ParameterName}_NumChars);
}

// Returns the total number of lines in InputText
void GetTextLineCount_{ParameterName}(out int Out_LineCount)
{
	Out_LineCount = int({ParameterName}_NumLines);
}

// Returns the 0-based line index for In_CharacterIndex in InputText
void GetCharacterLineNumber_{ParameterName}(in int In_CharacterIndex, out int Out_LineNumber)
{
	if (In_CharacterIndex >= 0 && In_CharacterIndex < int({ParameterName}_NumChars))
	{
		Out_LineNumber = int({ParameterName}_LineIndices[In_CharacterIndex]);
	}
	else
	{
		Out_LineNumber = 0;
	}
}

// Returns the number of characters in the specified line index
void GetLineCharacterCount_{ParameterName}(in int In_LineIndex, out int Out_LineCharacterCount)
{
	if (In_LineIndex >= 0 && In_LineIndex < int({ParameterName}_NumLines))
	{
		Out_LineCharacterCount = int({ParameterName}_LineCharacterCounts[In_LineIndex]);
	}
	else
	{
		Out_LineCharacterCount = 0;
	}
}


